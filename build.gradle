/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

group = 'com.fourponies.authentication.ldap-starttls'
version = '0.0.1'

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.fourponies.authentication.ldap-starttls',
        version    : project.version,
        goCdVersion: '16.2.1',
        name       : 'LDAP with STARTTLS Authentication Plugin for GoCD',
        description: 'The built-in LDAP authentication in GoCD does not support LDAP servers with STARTTLS. This plugin just rectifies that.',
        vendorName : 'GoCD Contributors',
        vendorUrl  : 'https://github.com/sprotheroe/gocd-ldap-starttls-authentication-plugin'
]

// Force 1.7 compatibility to ensure that it works with older JVMs
sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.96-beta'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.3.0'
    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }

//    This is useful for debugging
    from(sourceSets.main.java) {
        into "/"
    }
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked"
}

compileTestJava {
    options.compilerArgs << "-Xlint:unchecked"
}

